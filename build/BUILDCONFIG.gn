shared_binary_target_configs = [
  "//build:compiler_defaults",
  "//build:c++17",
]

# Apply that default list to the binary target types.
set_defaults("executable") {
  configs = shared_binary_target_configs
  configs += [ "//build:executable" ]
}
set_defaults("loadable_module") {
  configs = shared_binary_target_configs
}
set_defaults("static_library") {
  configs = shared_binary_target_configs
}
set_defaults("shared_library") {
  configs = shared_binary_target_configs
  configs += [ "//build:shared_library" ]
}
set_defaults("source_set") {
  configs = shared_binary_target_configs
}

if (target_os == "") {
  target_os = host_os
}
if (current_os == "") {
  current_os = target_os
}

if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_cpu == "") {
  current_cpu = target_cpu
}

if (host_os == "win") {
  host_toolchain = "//build/toolchain:win"
} else {
  host_toolchain = "//build/toolchain:unix"
}

set_default_toolchain(host_toolchain)
